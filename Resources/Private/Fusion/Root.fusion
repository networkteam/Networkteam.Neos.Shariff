prototype(Neos.Neos:Page) {
    body.javascripts.shariff = Neos.Fusion:Tag {
        tagName = 'script'

        attributes.src = Neos.Fusion:ResourceUri {
            path = 'resource://Networkteam.Neos.Shariff/Public/shariff/build/shariff.min.js'
        }
    }

    head.stylesheets.shariff = Neos.Fusion:Tag {
        tagName = 'link'

        attributes {
            rel = 'stylesheet'

            href = Neos.Fusion:ResourceUri {
                path = 'resource://Networkteam.Neos.Shariff/Public/shariff/build/shariff.complete.css'
            }
        }
    }
}

prototype(Networkteam.Neos.Shariff:Shariff) < prototype(Neos.Fusion:Tag) {
    // API
    @context {
        services = ${Configuration.setting('Networkteam.Neos.Shariff.options.services')}
        theme = ${Configuration.setting('Networkteam.Neos.Shariff.frontend.theme')}
        buttonStyle = ${Configuration.setting('Networkteam.Neos.Shariff.frontend.buttonStyle')}
        orientation = ${Configuration.setting('Networkteam.Neos.Shariff.frontend.orientation')}
        language = ${Configuration.setting('Networkteam.Neos.Shariff.frontend.language')}
    }

    attributes {
        class = 'shariff'
        data-backend-url = Neos.Fusion:UriBuilder {
            package = 'Networkteam.Neos.Shariff'
            format = 'json'
            controller = 'Shariff'
            action = 'counts'
        }
        data-services = ${String.toLowerCase(Json.stringify(services))}
        data-theme = ${theme}
        data-button-style = ${buttonStyle}
        data-orientation = ${orientation}
        data-lang = ${language}

        // Put additional attributes here, see https://github.com/heiseonline/shariff#options-data-attributes
    }

    @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
    @exceptionHandler = 'Neos\\Neos\\Fusion\\ExceptionHandlers\\NodeWrappingHandler'
}
